# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("./../uhdf.gni")

hdf_framework_path = "./../../../framework"
hdf_interfaces_path = "./../../../interfaces"

config("lib_utils_pub_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "$hdf_interfaces_path/inner_api/osal/shared",
    "$hdf_interfaces_path/inner_api/osal/uhdf",
    "$hdf_interfaces_path/inner_api/utils",
    "$hdf_interfaces_path/inner_api/core",
  ]
}
if (defined(ohos_lite)) {
  group("libpub_utils") {
    deps = []
  }
} else {
  ohos_shared_library("libpub_utils") {
    branch_protector_ret = "pac_ret"
    include_dirs = [
      "$hdf_framework_path/utils/include",
      "$hdf_uhdf_path/utils/include",
    ]
    public_configs = [ ":lib_utils_pub_config" ]
    sources = [
      "$hdf_framework_path/support/posix/src/osal_mem.c",
      "$hdf_framework_path/support/posix/src/osal_time.c",
      "$hdf_framework_path/utils/src/hdf_cstring.c",
      "$hdf_framework_path/utils/src/hdf_sbuf.c",
      "$hdf_framework_path/utils/src/hdf_sbuf_impl_raw.c",
      "$hdf_uhdf_path/utils/src/hdf_xcollie.cpp",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
    ]
    defines = []
    if (hicollie_enabled) {
      public_external_deps = [ "hicollie:libhicollie" ]
      defines += [ "HDFHICOLLIE_ENABLE" ]
    }

    install_images = [
      system_base_dir,
      "updater",
    ]
    innerapi_tags = [
      "chipsetsdk",
      "platformsdk_indirect",
    ]

    subsystem_name = "hdf"
    part_name = "hdf_core"
  }
}
