# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("./../uhdf.gni")

hdf_framework_path = "./../../../framework"
hdf_interface_path = "./../../../interfaces"

config("libhdf_ipc_adapter_pub_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "$hdf_interface_path/inner_api/ipc",
    "$hdf_interface_path/inner_api/core",
    "$hdf_uhdf_path/utils/include",
  ]
}
if (defined(ohos_lite)) {
  group("libhdf_ipc_adapter") {
    deps = []
  }
} else {
  ohos_shared_library("libhdf_ipc_adapter") {
    branch_protector_ret = "pac_ret"
    include_dirs = [
      "include",
      "$hdf_framework_path/utils/include",
      "$hdf_framework_path/core/shared/include",
    ]

    public_configs = [ ":libhdf_ipc_adapter_pub_config" ]

    sources = [
      "src/hdf_dump.cpp",
      "src/hdf_remote_adapter.cpp",
      "src/hdf_remote_service.c",
      "src/hdf_sbuf_impl_hipc.cpp",
    ]

    if (is_standard_system) {
      deps = [ "../pub_utils:libpub_utils" ]
      external_deps = [
        "c_utils:utils",
        "hilog:libhilog",
        "ipc:ipc_single",
        "samgr:samgr_proxy",
      ]
    } else {
      external_deps = [
        "hilog:libhilog",
        "ipc:ipc_single",
        "samgr:samgr_proxy",
      ]
    }

    if (build_selinux) {
      cflags = [ "-DWITH_SELINUX" ]
    }

    innerapi_tags = [
      "chipsetsdk",
      "platformsdk_indirect",
    ]
    install_images = [
      system_base_dir,
      updater_base_dir,
    ]
    subsystem_name = "hdf"
    part_name = "hdf_core"
  }
}
