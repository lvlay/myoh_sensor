# Copyright (c) 2021 - 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//drivers/interface/camera/camera.gni")

config("camera_metadata_config") {
  include_dirs = [ "./include" ]
}

ohos_shared_library("metadata") {
  branch_protector_ret = "pac_ret"

  install_enable = true
  cflags = [
    "-fPIC",
    "-Wall",
  ]
  if (drivers_interface_camera_feature_coverage) {
    cflags += [ "--coverage" ]
    ldflags = [ "--coverage" ]
    cflags_cc = cflags
  }
  all_dependent_configs = [ ":camera_metadata_config" ]
  if (defined(ohos_lite)) {
    sources = [
      "src/camera_metadata_info.cpp",
      "src/camera_metadata_operator.cpp",
    ]
    external_deps = [ "hilog_lite:hilog_shared" ]
  } else {
    sources = [
      "src/camera_metadata_info.cpp",
      "src/camera_metadata_operator.cpp",
      "src/metadata_utils.cpp",
    ]

    if (drivers_interface_camera_feature_vendor_tag == true) {
      defines = []
      defines += [ "CAMERA_VENDOR_TAG" ]
    }

    deps = [ "../v1_0:libcamera_proxy_1.0" ]
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  }

  install_images = [ system_base_dir ]
  innerapi_tags = [
    "chipsetsdk",
    "platformsdk_indirect",
  ]
  public_configs = [ ":camera_metadata_config" ]
  part_name = "drivers_interface_camera"
  subsystem_name = "hdf"
}
